@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

include( "${CMAKE_CURRENT_LIST_DIR}/wisdom-shared-targets.cmake" )
if (CMAKE_VERSION VERSION_GREATER_EQUAL "3.28")
	include( "${CMAKE_CURRENT_LIST_DIR}/wisdom-shared-module-targets.cmake" )
endif()

if(WIN32)
	set(DXC_EXECUTABLE "@PACKAGE_CMAKE_INSTALL_BINDIR@/dxc.exe")
	set(DXAGILITY_DLL "@PACKAGE_CMAKE_INSTALL_BINDIR@/D3D12Core.dll")
	set(DXAGILITY_DEBUG_DLL "@PACKAGE_CMAKE_INSTALL_BINDIR@/d3d12SDKLayers.dll")

	include( "${CMAKE_CURRENT_LIST_DIR}/wisdom-dx12-targets.cmake" )
	if (CMAKE_VERSION VERSION_GREATER_EQUAL "3.28")
		include( "${CMAKE_CURRENT_LIST_DIR}/wisdom-dx12-module-targets.cmake" )
	endif()

	find_dependency(Vulkan QUIET)
else()
	find_dependency(Vulkan REQUIRED) # without Vulkan there is no target for linux
	set(DXC_EXECUTABLE "@PACKAGE_CMAKE_INSTALL_BINDIR@/dxc")
endif()

if(Vulkan_FOUND)
	find_dependency(WisVk REQUIRED PATHS "${CMAKE_CURRENT_LIST_DIR}/../WisVk/")

	include( "${CMAKE_CURRENT_LIST_DIR}/wisdom-vk-targets.cmake" )
	if (CMAKE_VERSION VERSION_GREATER_EQUAL "3.28")
		include( "${CMAKE_CURRENT_LIST_DIR}/wisdom-vk-module-targets.cmake" )
	endif()
else()
	# add empty targets to disable vulkan on platform code
	add_library(wisdom-vk INTERFACE)
	add_library(wis::wisdom-vk ALIAS wisdom-vk)
	add_library(wis::wisdom-vk-headers ALIAS wisdom-vk)
endif()
include( "${CMAKE_CURRENT_LIST_DIR}/wisdom-deps.cmake" )
include( "${CMAKE_CURRENT_LIST_DIR}/wisdom-targets.cmake" )

add_alias_target(wis::headers wis::wisdom-headers)
if(WISDOM_CPP_MODULES_SUPPORTED)
	include( "${CMAKE_CURRENT_LIST_DIR}/wisdom-module-targets.cmake" )
	add_alias_target(wis::module wis::wisdom-module)
endif()


include( "${CMAKE_CURRENT_LIST_DIR}/wisdom-platform-config.cmake" )
